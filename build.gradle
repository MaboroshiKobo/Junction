plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.1'
    id 'com.diffplug.spotless' version '8.2.1'
}

group = 'org.maboroshi'
version = '1.5.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url = "https://repo.opencollab.dev/main/"
    }
    maven {
        url = "https://repo.codemc.io/repository/creatorfromhell/"
    }
    maven {
        url = "https://repo.extendedclip.com/releases/"
    }
    maven {
        url = "https://jitpack.io/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")
    compileOnly("de.exlll:configlib-paper:4.8.1")
    implementation("org.bstats:bstats-bukkit:3.2.1")
    compileOnly("org.geysermc.floodgate:api:2.2.5-SNAPSHOT")
    compileOnly("net.milkbowl.vault:VaultUnlockedAPI:2.19")
    compileOnly("com.github.MilkBowl:VaultAPI:1.7.1")
    compileOnly("net.luckperms:api:5.5")
    compileOnly("me.clip:placeholderapi:2.12.2")
}

tasks.named('shadowJar') {
  relocate 'org.bstats', 'org.maboroshi.junction.libs.bstats'
  exclude 'plugin.yml'
  minimize()
  archiveClassifier.set('')
}

build.dependsOn shadowJar

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

spotless {
  format 'misc', {
    target '*.gradle', '.gitattributes', '.gitignore'
    trimTrailingWhitespace()
    leadingTabsToSpaces()
    endWithNewline()
  }

  java {
    target 'src/*/java/**/*.java'
    palantirJavaFormat('2.83.0')
    formatAnnotations()
    importOrder()
    removeUnusedImports()
    cleanthat()
  }
}
